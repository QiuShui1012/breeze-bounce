plugins {
	id "com.github.johnrengelman.shadow" version "7.1.2"
}

architectury {
	platformSetupLoomIde()
	fabric()
}

configurations {
	common
	shadowCommon // Don't use shadow from the shadow plugin since it *excludes* files.
	compileClasspath.extendsFrom common
	runtimeClasspath.extendsFrom common
	developmentFabric.extendsFrom common
}

loom {
	Project common = project(":common")
	accessWidenerPath = common.loom.accessWidenerPath

	runs {
		datagen {
			client()

			name "Minecraft Data"
			vmArg "-Dfabric-api.datagen"
			vmArg "-Dfabric-api.datagen.output-dir=${common.file("src/generated/resources")}"
			vmArg "-Dfabric-api.datagen.modid=${archives_base_name}"
			//vmArg "-Dporting_lib.datagen.existing_resources=${common.file("src/main/resources")}"

			environmentVariable("DATAGEN", "TRUE")
		}
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"

	modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"

	common(project(path: ":common", configuration: "namedElements")) { transitive false }
	shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }
}

processResources {

}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

shadowJar {
	exclude "architectury.common.json"

	configurations = [project.configurations.shadowCommon]
	archiveClassifier = "dev-shadow"
}

remapJar {
	injectAccessWidener = true
	input.set shadowJar.archiveFile
	dependsOn shadowJar

}

sourcesJar {
	def commonSources = project(":common").sourcesJar
	dependsOn commonSources
	from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
	withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
		skip()
	}
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}

var env = System.getenv()

var changelogFile = rootProject.file("changelog.md")
var changelogText = changelogFile.exists() ? changelogFile.text : ""
var vName = mod_name + " " + project.version

if (env.MODRINTH_TOKEN) {
	modrinth {
		token = System.getenv("MODRINTH_TOKEN") // This is the default. Remember to have the MODRINTH_TOKEN environment variable set or else this will fail, or set it to whatever you want - just make sure it stays private!
		projectId = modrinth_project_id // This can be the project ID or the slug. Either will work!
		versionName = vName
		versionNumber = project.version // You don't need to set this manually. Will fail if Modrinth has this version already
		versionType = mod_version_type // This is the default -- can also be `beta` or `alpha`
		changelog = changelogText
		uploadFile = remapJar // With Loom, this MUST be set to `remapJar` instead of `jar`!
		gameVersions = minecraft_supported_versions.split(",").toList()
		loaders = ["fabric","quilt"]
		dependencies {
			// scope.type
			// The scope can be `required`, `optional`, `incompatible`, or `embedded`
			// The type can either be `project` or `version`
			required.project "fabric-api"
		}
	}
}

if (env.CURSEFORGE_TOKEN) {
	curseforge {
		apiKey = env.CURSEFORGE_TOKEN
		project {
			id = curseforge_project_id
			changelog = changelogText
			changelogType = 'markdown'
			releaseType = mod_version_type
			for (String v : minecraft_supported_versions.split(",").toList()) {
				addGameVersion v
			}
			addGameVersion "Fabric"
			addGameVersion "Quilt"

			mainArtifact(remapJar) {
				displayName = vName
			}

			relations {
				requiredDependency "fabric-api"
			}
		}
	}
}

unifiedPublishing {
	project {
		displayName = vName
		version = project.version
		changelog = changelogText
		releaseType = mod_version_type
		gameVersions = minecraft_supported_versions.split(",").toList()
		gameLoaders = ["fabric","quilt"]

		mainPublication tasks.remapJar

		relations {
			depends {
				curseforge = "fabric-api"
				modrinth = "fabric-api"
			}
			includes {} // Mark as an included dependency
		}

		var cfToken = System.getenv("CURSEFORGE_TOKEN")
		if (cfToken != null) {
			curseforge {
				token = cfToken
				id = curseforge_project_id
			}
		}

		var mrToken  = System.getenv("MODRINTH_TOKEN")
		if (mrToken != null) {
			modrinth {
				token = mrToken
				id = modrinth_project_id
			}
		}
	}
}